* Credits
- Author :: https://github.com/philippe-grenet
- URL :: https://github.com/philippe-grenet/exordium/blob/e7b733fc5b065e173112e59f0a7995c37631b100/modules/init-bde-style.el
- Accessed :: <2018-05-17 Thu>
* Overview (reformatted from the original)
This section define a C style named "bde" using c-add-style.  The offset
in the specification (c-offset-alist) can be any of the following:
- An integer -> specifies a relative offset. All relative offsets will be
  added together and used to calculate the indentation relative to an
  anchor position earlier in the buffer.
- One of the symbols ~+~, ~-~, ~++~, ~--~, ~*~, or ~/~
  | Symbol | Equal to c-basic-offset times... |
  |--------+----------------------------------|
  | ~+~      |                                1 |
  | ~-~      |                               -1 |
  | ~++~     |                                2 |
  | ~--~     |                               -2 |
  | ~*~      |                               .5 |
  | ~/~      |                              -.5 |

NOTE: to debug the indentation of a particular line, type 'C-c C-s'. It
will display the variable 'c-syntactic-context' which is a list of the
syntactic components affect the offset calculations for that line, with the
character position in the buffer for each of them. More details in M-x
info, then CC mode, then Interactive Customization.
See cc-align.el for examples of line-up functions.
* Config
#+BEGIN_SRC emacs-lisp
(eval-when-compile (defvar c-syntactic-context))

(defun bde-is-member-function-declaration ()
  "Return whether the line ending resembles the member function declaration."
  (re-search-forward
   (concat ") *\\(const\\)?"
           " *\\(noexcept\\|BSLS_CPP11_NOEXCEPT\\)?"
           " *\\(\\(= *\\(0\\|de\\(fault\\|lete\\)\\)\\)"
           "\\|BSLS_CPP11_DE\\(FAULT\\|LETED\\)"
           "\\|override\\|BSLS_CPP11_OVERRIDE\\)?"
           " *\\(&\\(&\\)?\\)?"
           " *; *$")
           (point-at-eol) t))

(defun bde-comment-offset (element)
  "Custom line-up function for BDE comments.
Return a symbol for the correct indentation level at the
current cursor position, if the cursor is within a class definition:
1. + for method comments:
        int foo() const = 0;
            // tab goes here
        int bar() { return 0; }
            // tab goes here
2. column number of beginning of comment for data member comments:
        int d_data;     // my comment at whatever column I want
                        // tab goes here
        int d_someLongVariableName;
                        // my comment at whatever column I want
                        // tab goes here
3. nil otherwise."
  (case (caar c-syntactic-context)
    ((inclass innamespace)
     (save-excursion
       (let ((class-offset         ; extra offset for inner structs
              (c-langelem-col (car c-syntactic-context) t))
             (comment-column nil)) ; column number of last //
         (loop
          (beginning-of-line)
          (cond ((= (point) (point-min))
                 (return nil))
                ((re-search-forward "^ *//" (point-at-eol) t)
                 ;; looking at a comment line
                 (setq comment-column (- (current-column) 2))
                 (forward-line -1))
                ((bde-is-member-function-declaration)
                 ;; looking at end of method declaration
                 (return '+))
                ((re-search-forward "} *$" (point-at-eol) t)
                 ;; looking at end of inline method definition
                 (return '+))
                ((re-search-forward "; *//" (point-at-eol) t)
                 ;; looking at beginning of data member comment block
                 (return (- (current-column) 2 class-offset c-basic-offset)))
                ((and comment-column
                      (re-search-forward "[_A-Za-z0-9]+; *$"
                                         (point-at-eol) t))
                 ;; looking at end of (long?) data member declaration
                 (return (- comment-column class-offset c-basic-offset)))
                (t
                 (return nil)))))))
    (t nil)))

(defun bde-statement-block-intro-offset (element)
  "Custom line-up function for first line of a statement block.
The default identation is is '+' (1 basic offset), unless we are in
a switch statement, in which case the indentation is set to
'*' (half basic offset). Example:
switch(val) {
  case 100: {
      return 1;
  } break;
  default: {
      return 0;
  } break;
}"
  (save-excursion
    (goto-char (c-langelem-pos element))
    (if (looking-at "\\(case\\|default\\)")
        '*
      '+)))

;; See http://cc-mode.sourceforge.net/html-manual/Syntactic-Symbols.html#Syntactic-Symbols
(c-add-style
 "bde"
 '((c-basic-offset . 4)
   (c-comment-only-line-offset . 0)
   (fill-column . 79)
   (c-backslash-column . 78)
   (c-backslash-max-column . 78)
   (c-offsets-alist
    (comment-intro         . bde-comment-offset)
    (defun-open            . 0)
    (defun-close           . 0)
    (statement-block-intro . bde-statement-block-intro-offset)
    (substatement-open     . 0)
    (substatement-label    . 0)
    (label                 . 0)
    (access-label          . /)
    (case-label            . *)
    (statement-case-intro  . *)
    (statement-case-open   . 0)
    (statement-cont        . +)
    (inline-open           . 0)
    (inline-close          . 0)
    (innamespace           . 0)
    (member-init-intro     . 0)
    (extern-lang-open      . 0)
    (brace-list-entry      . /)
    (extern-lang-close     . 0))))
#+END_SRC
